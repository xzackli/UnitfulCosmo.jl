module UnitfulCosmo
# based heavily on Mason Protter's NaturallyUnitful.jl

using Reexport
@reexport using Unitful
@reexport using UnitfulAstro
import PhysicalConstants.CODATA2018: SpeedOfLightInVacuum, ReducedPlanckConstant,
    BoltzmannConstant, NewtonianConstantOfGravitation, VacuumElectricPermittivity

const c  = float(SpeedOfLightInVacuum)
const Ä§  = float(ReducedPlanckConstant)
const G = float(NewtonianConstantOfGravitation)
const kB = float(BoltzmannConstant)
const Îµ0 = float(VacuumElectricPermittivity)

setDimPow(D::Dict, x::Unitful.Dimension{T}) where {T} = D[T] = x.power

function dimDict(x::Unitful.Dimensions{T}) where {T}
    D = Dict(:Length => 0,
             :Mass => 0,
             :Temperature => 0,
             :Time => 0,
             :Current => 0)
    for t in T
        setDimPow(D,t)
    end
    return D
end
dimDict(x::Unitful.Quantity{T}) where {T} = dimDict(dimension(x))
dimDict(x) = Dict(:Length => 0, :Mass => 0, :Temperature => 0, :Time => 0, :Current => 0)


"""
    mpc(q; base=u"Mpc")

Convert `q` to Mpc units based on the units specified by `base`. If `base` is
unspecified, `mpc` will default to `Mpc`. You could choose some other length unit
if you wanted.
"""
mpc(q; base=u"Mpc") = _mpc(base, q)

function _mpc(base::Unitful.FreeUnits{T, Unitful.ð‹, U}, q) where {T, U}
    D = dimDict(q)
    (Î±,Î²,Î³,Î´,Ï•) = (D[:Length], D[:Mass], D[:Temperature], D[:Time], D[:Current])
    uconvert(base^(Î±+Î´-Î²-Î³-Ï•),
            q * (c/Ä§)^Î² * (kB/(c*Ä§))^Î³ * c^Î´ * (c^3*Îµ0*Ä§)^(-Ï•/2))
end

function _mpc(base::Unitful.FreeUnits{T, U, V}, q) where {T, U, V}
    throw(ArgumentError(
        """mpc(q; base)` where `base` has dimensions `$(U)` has not yet been implemented. Please use a base with dimensions of `length` and then use `unmpc` to convert to your desired units."""
        )
    )
end

"""
    unmpc(targetUnit, q)

Convert a quantity `q` to units specified by `targetUnits` automatically inserting whatever
factors  of `Ä§`, `c`, `Ïµâ‚€` and `kb` are required to make the conversion work.
"""
function unmpc(targetUnit, q)
    natTarget = mpc(1targetUnit)
    natQ = mpc(q)
    ratio = natQ/natTarget
    if typeof(ratio |> dimension) == Unitful.Dimensions{()}
        (ratio)targetUnit
    else
        throw(Unitful.DimensionError(q,targetUnit))
    end
end


"""
    planck(q)

Strip units by inserting whatever `Ä§`, `c`, `G` and `kb` are required to make the conversion work.
"""
function planck(q)
    D = dimDict(q)
    (Î±,Î²,Î³,Î´,Ï•) = (D[:Length], D[:Mass], D[:Temperature], D[:Time], D[:Current])
    return uconvert(Unitful.NoUnits,
        q *
        (G * Ä§ / c^3)^(-Î±/2) *  # length
        (c * Ä§ / G)^(-Î²/2) *    # mass
        (c^5 * Ä§ / (G * kB^2))^(-Î³/2) *  # temperature
        (G * Ä§ / c^5)^(Ï•/2 - Î´/2) *  # time and current
        (4Ï€ * Îµ0 * Ä§ * c)^(-Ï•/2))  # charge
end

"""
    unplanck(targetUnit, q)

Convert a quantity `q` to units specified by `targetUnits` automatically inserting whatever
factors  of `Ä§`, `c`, `G` and `kb` are required to make the conversion work.
"""
function unplanck(targetUnit, q)
    if unit(q) != unit(1)
        throw(Unitful.DimensionError(q,Unitful.NoUnits))
    end
    natTarget = planck(1targetUnit)
    natQ = planck(q)
    ratio = natQ/natTarget
    (ratio)targetUnit
end


export planck, unplanck
export mpc, unmpc

end
